buildscript {
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
        classpath "io.freefair.gradle:gradle-plugins:0.10.0"
        classpath "io.freefair.gradle:android-gradle-plugins:2.3.1"
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url "https://maven.google.com" }
        maven { url "https://jitpack.io" }
    }
    it.group = 'io.freefair'

    apply plugin: 'io.freefair.git-version'
}

subprojects { Project sp ->
    sp.pluginManager.withPlugin('com.android.library') {
        sp.with {
            apply plugin: 'io.freefair.android-maven-jars'
            apply plugin: 'io.freefair.android-ci'

            android {
                compileSdkVersion 25
                buildToolsVersion "25.0.3"

                defaultConfig {
                    minSdkVersion 1
                    targetSdkVersion 25
                    versionCode 1
                    versionName project.version
                }
            }

            apply plugin: 'com.github.dcendents.android-maven'

            dependencies {
                testCompile 'junit:junit:4.12'
            }
        }
    }
}
