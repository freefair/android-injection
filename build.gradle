buildscript {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
        classpath 'io.freefair:gradle-plugins:2.0.0-beta6'
        classpath 'io.freefair:android-gradle-plugins:2.1.2'
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }
    it.group = 'io.freefair'

    apply plugin: 'io.freefair.git-version'
}

def isCi = "true".equals(System.getenv("CI"))

configure(subprojects - project(':modules')) {
    apply plugin: 'com.android.library'
    apply plugin: 'io.freefair.android-maven-jars'

    android {
        compileSdkVersion 23
        buildToolsVersion "23.0.3"

        defaultConfig {
            minSdkVersion 1
            targetSdkVersion 21
            versionCode 1
            versionName project.version
        }

        lintOptions {
            xmlReport false
            htmlReport !isCi
            textOutput 'stdout'
            textReport isCi
        }
    }

    apply plugin: 'com.github.dcendents.android-maven'

    dependencies {
        testCompile 'junit:junit:4.12'
    }
}
