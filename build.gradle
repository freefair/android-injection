buildscript {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
        classpath 'io.freefair:gradle-plugins:1.0.1'
    }
}

apply plugin: 'io.freefair.git-version'

allprojects {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }
    it.group = 'io.freefair'
    version = rootProject.version
}

def isCi = "true".equals(System.getenv("CI"))

configure(subprojects - project(':modules')) {
    apply plugin: 'com.android.library'
    apply plugin: 'io.freefair.android-maven-install'

    android {
        compileSdkVersion 23
        buildToolsVersion "23.0.2"

        defaultConfig {
            minSdkVersion 1
            versionCode 1
            versionName project.version
        }

        lintOptions {
            xmlReport false
            htmlReport !isCi
            textOutput 'stdout'
            textReport isCi
        }
    }

    apply plugin: 'com.github.dcendents.android-maven'

    dependencies {
        testCompile 'junit:junit:4.12'
    }
}
